
constant N := 4;

variable x[Nat % N] <- Nat % 5;

// There exists one token in the ring.
(future & shadow)
  (unique i <- Nat % N :
   i==0
   &&
   (0==1
    || x[i-1]> 1 && x[i]> 1
    || x[i-1]<=1 && x[i]==0
    || x[i-1]==1 && x[i]==1
   )
   ||
   i!=0
   &&
   (0==1
    || x[i-1]==0 && x[i]> 1
    || x[i-1]==1 && x[i]!=1
    || x[i-1]>=2 && x[i]<=1
   )
  );

process Bot[i <- Nat % 1]
{
  read:  x[i-1];
  write: x[i];

  direct action:
    ( x[i-1]==0 && x[i]==0 --> x[i]:=1; )
    ( x[i-1]==1 && x[i]<=1 --> x[i]:=2; )
    ( x[i-1]> 1 && x[i]> 1 --> x[i]:=0; )
    ;
}

process P[j <- Nat % (N-1)]
{
  let i := j+1;

  read:  x[i-1];
  write: x[i];

  direct action:
    ( x[i-1]==0 && x[i]> 1 --> x[i]:=x[i]/4; )
    ( x[i-1]==1 && x[i]!=1 --> x[i]:=1; )
    ( x[i-1]==2 && x[i]<=1 --> x[i]:=2+x[i]; )
    ( x[i-1]>=3 && x[i]<=1 --> x[i]:=4; )
    ;
}

