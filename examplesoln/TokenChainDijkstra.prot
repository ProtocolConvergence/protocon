
// Token passing on a chain defined in
// Title: Self-stabilizing Systems in Spite of Distributed Control
// Author: Edsger W. Dijkstra
// Year: 1974

constant N := 3;

variable up[N] < 2;
variable z[N] < 2;

// One process has a token.
(future & shadow)
  (unique i < N : false
   || i==0        && z[i]==z[i+1] && up[i+1]==0
   || (i+1)%N > 1 && z[i-1]!=z[i]
   || (i+1)%N > 1 && z[i-1]==z[i] && up[i]==1 && z[i]==z[i+1] && up[i+1]==0
   || i==N-1      && z[i-1]!=z[i]
  );

process Bot[i < 1]
{
  write: up[i], z[i];
  read:  up[i+1], z[i+1];

  (assume & closed) (up[i]==1);

  action:
    ( z[i]==z[i+1] && up[i+1]==0 --> z[i]:=1-z[i+1]; );
}

process P[i <- map tid < N-2 : tid+1]
{
  read: z[i-1];
  write: up[i], z[i];
  read:  up[i+1], z[i+1];
  action:
    ( z[i-1]!=z[i] --> z[i]:=z[i-1]; up[i]:=1; )
    ( z[i-1]==z[i] && up[i]==1 && z[i]==z[i+1] && up[i+1]==0 --> up[i]:=0; )
    ;
}

process Top[i <- map tid < 1 : N-1]
{
  read: z[i-1];
  write: up[i], z[i];

  (assume & closed) (up[i]==0);

  action:
    ( z[i-1]!=z[i] --> z[i]:=z[i-1]; );
}

