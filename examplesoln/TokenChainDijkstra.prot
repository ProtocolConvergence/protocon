
// Token passing on a chain defined in
// Title: Self-stabilizing Systems in Spite of Distributed Control
// Author: Edsger W. Dijkstra
// Year: 1974

constant N := 5;

variable up[Int % N] <- Int % 2;
variable z[Int % N] <- Int % 2;

// One process has a token.
(future & shadow)
  (unique i <- Int % N : 0==1
   || i==0        && z[i]==z[i+1] && up[i+1]==0
   || (i+1)%N > 1 && z[i-1]!=z[i]
   || (i+1)%N > 1 && z[i-1]==z[i] && up[i]==1 && z[i]==z[i+1] && up[i+1]==0
   || i==N-1      && z[i-1]!=z[i]
  );

process Bot[i <- Int % 1]
{
  write: up[i], z[i];
  read:  up[i+1], z[i+1];

  (assume & closed) (up[i]==1);

  action:
    ( z[i]==z[i+1] && up[i+1]==0 --> z[i]:=1-z[i+1]; );
}

process P[j <- Int % (N-2)]
{
  let i := j+1;
  read: z[i-1];
  write: up[i], z[i];
  read:  up[i+1], z[i+1];
  action:
    ( z[i-1]!=z[i] --> z[i]:=z[i-1]; up[i]:=1; )
    ( z[i-1]==z[i] && up[i]==1 && z[i]==z[i+1] && up[i+1]==0 --> up[i]:=0; )
    ;
}

process Top[j <- Int % 1]
{
  let i := N-1;
  read: z[i-1];
  write: up[i], z[i];

  (assume & closed) (up[i]==0);

  action:
    ( z[i-1]!=z[i] --> z[i]:=z[i-1]; );
}

