
// Distance-2 coloring.
// Processes that are 1 or 2 hops away must have different colors.

constant N := 3;
constant M := 5;

direct variable x[Nat % N] <- Nat % M;
random variable b[Nat % N] <- Nat % 2;

// Each process P[i] must have a different color than P[i-1] and P[i-2].
// This implies that P[i] also has a different color than P[i+1] and P[i+2].
(future & future silent)
  (forall i <- Nat % N : x[i]!=x[i-1] && x[i]!=x[i-2]);

process P[i <- Nat % N]
{
  read: x[i-1];
  write: x[i];

  // This variable is randomized when ANY process acts.
  read: b[i];

  puppet action:
    ( x[i-1] <= 2 -=> x[i] := 1 + x[i-1]; )
    ( x[i-1] >= 3 && x[i]!=1 && x[i]!=(x[i-1]+1)%M --> x[i] := 1 + x[i-1] * b[i]; )
    ;
}

