
// Leader election in a prime-sized ring defined in
// Title: Leader Election in Uniform Rings
// Author: Shing-Tsaan Huang
// Year: 1993

constant N := 5;
constant M := N;

variable x[N] < M;

process P[i < N]
{
  read:  x[i-1];
  write: x[i];
  read: x[i+1];
  puppet action:
    ( x[i-1] == x[i] && x[i] == x[i+1] --> x[i] := x[i] + 1; )
    ( x[i-1] != x[i] && x[i] == x[i+1] --> x[i] := x[i] + 1; )
    ( x[i-1] != x[i] && x[i] != x[i+1] && (x[i]-x[i-1]) % M < (x[i+1]-x[i]) % M --> x[i] := x[i] + 1; )
    ;
}

// For prime-sized rings, any solution leaves all x values unique.
// A leader can be chosen by finding the process whose x[i]==0.
(future & silent)
(
  (forall i < N :
   (x[i-1] - x[i]) % M == (x[i] - x[i+1]) % M
  )
  &&
  // All values are unique.
  (forall i < N :
   (forall j < N :
    i == j || x[i] != x[j]
   )
  )
);

