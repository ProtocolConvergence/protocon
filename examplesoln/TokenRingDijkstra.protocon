
// Token passing around a ring defined in
// Title: Self-stabilizing Systems in Spite of Distributed Control
// Author: Edsger W. Dijkstra
// Year: 1974

constant N := 5;
constant M := N;

variable x[Int % N] <- Int % M;

process Bot[i <- Int % 1]
{
  read:  x[i-1];
  write: x[i];
  direct action:
    ( x[i-1] == x[i] --> x[i] := x[i-1] + 1; );
}

process P[j <- Int % (N-1)]
{
  let i := j+1;
  read:  x[i-1];
  write: x[i];
  direct action:
    ( x[i-1] != x[i] --> x[i] := x[i-1]; );
}

invariant:
  unique i <- Int % N :
  (   i == 0 && x[i-1] == x[i]
   || i != 0 && x[i-1] != x[i]
  );

