
// Token passing on a ring defined in
// Title: A Self-Stabilizing Algorithm with Tight Bounds for Mutual Exclusion on a Ring
// Author: Viacheslav Chernoy
// Author: Mordechai Shalom
// Author: Shmuel Zaks
// Year: 2008

constant N := 3;

variable z[N] < 3;

process Bot[i < 1]
{
  write: z[i];
  read:  z[i+1];
  direct action:
    ( (z[i]+1)%3 == z[i+1] --> z[i] := z[i]+2; )
    ;
}

process P[i <- map tid < N-2 : tid+1]
{
  read:  z[i-1];
  write: z[i];
  read:  z[i+1];
  direct action:
    (    ((z[i-1]-1)%3 == z[i+1] && z[i+1] == z[i])
      || ((z[i  ]+1)%3 == z[i-1] && z[i-1] == z[i+1])
      || ((z[i+1]-1)%3 == z[i-1] && z[i-1] == z[i])
     -->
      z[i] := z[i] + 1;
    )
    ;
}


process Top[i <- map tid < 1 : N-1]
{
  read: z[i-1];
  write: z[i];
  read: z[i+1];
  direct action:
    ( z[i-1] == z[i] && z[i] == z[i+1] || z[i-1] == (z[i]+1)%3 --> z[i] := z[i-1]+1; )
    ;
}

(future & shadow)
(
  (exists a < 3 :
   (forall i < N :
    z[i] != a))
  &&
  // One process has a token.
  (unique i < N :
   (   (i == 0 &&
        ((z[i]+1)%3 == z[i+1]))
    || (i != 0 && i != N-1 &&
        (   ((z[i-1]-1)%3 == z[i+1] && z[i+1] == z[i])
         || ((z[i  ]+1)%3 == z[i-1] && z[i-1] == z[i+1])
         || ((z[i+1]-1)%3 == z[i-1] && z[i-1] == z[i])))
    || (i == N-1 &&
        (z[i-1] == z[i] && z[i] == z[i+1] || z[i-1] == (z[i]+1)%3))
   )
  )
);

