
// Token passing on a ring defined in
// Title: A Self-Stabilizing Algorithm with Tight Bounds for Mutual Exclusion on a Ring
// Author: Viacheslav Chernoy
// Author: Mordechai Shalom
// Author: Shmuel Zaks
// Year: 2008

constant N := 3;

variable z[Int % N] <- Int % 3;

process Bot[i <- Int % 1]
{
  write: z[i];
  read:  z[i+1];
  direct action:
    ( (z[i]+1)%3 == z[i+1] --> z[i] := z[i]+2; )
    ;
}

process P[j <- Int % (N-2)]
{
  let i := j+1;
  read:  z[i-1];
  write: z[i];
  read:  z[i+1];
  direct action:
    (    ((z[i-1]-1)%3 == z[i+1] && z[i+1] == z[i])
      || ((z[i  ]+1)%3 == z[i-1] && z[i-1] == z[i+1])
      || ((z[i+1]-1)%3 == z[i-1] && z[i-1] == z[i])
     -->
      z[i] := z[i] + 1;
    )
    ;
}


process Top[j <- Int % 1]
{
  let i := N-1;
  read: z[i-1];
  write: z[i];
  read: z[i+1];
  direct action:
    ( z[i-1] == z[i] && z[i] == z[i+1] || z[i-1] == (z[i]+1)%3 --> z[i] := z[i-1]+1; )
    ;
}

(future & shadow)
(
  (exists a <- Int % 3 :
   (forall i <- Int % N :
    z[i] != a))
  &&
  // One process has a token.
  (unique i <- Int % N :
   (   (i == 0 &&
        ((z[i]+1)%3 == z[i+1]))
    || (i != 0 && i != N-1 &&
        (   ((z[i-1]-1)%3 == z[i+1] && z[i+1] == z[i])
         || ((z[i  ]+1)%3 == z[i-1] && z[i-1] == z[i+1])
         || ((z[i+1]-1)%3 == z[i-1] && z[i-1] == z[i])))
    || (i == N-1 &&
        (z[i-1] == z[i] && z[i] == z[i+1] || z[i-1] == (z[i]+1)%3))
   )
  )
);

