
// Token ring adapted from examplesoln/TokenRingFiveState.prot.

constant N := 3;
constant M := 5;

shadow variable tok[N] < 2;
puppet variable x[N] < M;

// There exists one token in the ring.
// Also, every action should pass a token (see shadow actions below).
((future & active shadow) % puppet)
  (unique i < N : tok[i]==1);

process Bot[i < 1]
{
  read:  x[i-1];
  write: x[i];

  write: tok[i];
  write: tok[i+1];

  shadow action:
    ( tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; );

  puppet action:
    ( x[i-1]==0 && x[i]==0 --> x[i]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==1 && x[i]<=1 --> x[i]:=2; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]> 1 && x[i]> 1 --> x[i]:=0; tok[i]:=0; tok[i+1]:=1; )
    ;
}

process P[i <- map tid < N-1 : tid+1]
{
  read:  x[i-1];
  write: x[i];

  write: tok[i];
  write: tok[i+1];

  shadow action:
    ( tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; );

  puppet action:
    ( x[i-1]==0 && x[i]> 1 --> x[i]:=x[i]/4; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==1 && x[i]!=1 --> x[i]:=1;      tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==2 && x[i]<=1 --> x[i]:=2+x[i]; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]>=3 && x[i]<=1 --> x[i]:=4;      tok[i]:=0; tok[i+1]:=1; )
    ;
}

