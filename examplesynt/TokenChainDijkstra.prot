
// This version contains some tweaks from Dijkstra's original protocol for the
// sake of synthesis. See examplesoln/TokenChainDijkstra.prot for his version.

constant N := 3;

shadow variable fwd[Nat % 1] <- Nat % 2;
shadow variable tok[Nat % N] <- Nat % 2;
puppet variable up[Nat % N] <- Nat % 2;
puppet variable z[Nat % N] <- Nat % 2;

((future & active shadow) % puppet)
  (unique i <- Nat % N : tok[i]==1);

process Bot[i <- Nat % 1]
{
  write: fwd[0], tok[i], tok[i+1];

  write: up[i], z[i];
  read:  up[i+1], z[i+1];

  shadow:
    ( tok[i]==1 --> fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; );

  puppet:
    ( up[i]==0 || up[i+1]==0
     -=>
      z[i]:=1-z[i+1];
      up[i]:=1;
      fwd[0]:=1; tok[i]:=0; tok[i+1]:=1;
    );
}

process P[i <- 1 + Nat % (N-2)]
{
  read: fwd[0];
  write: tok[i-1], tok[i], tok[i+1];

  write: up[i];
  read:  up[i+1];

  read: z[i-1];
  write: z[i];
  read: z[i+1];

  shadow:
    ( fwd[0]==1 && tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; )
    ( fwd[0]==0 && tok[i]==1 --> tok[i-1]:=1; tok[i]:=0; )
    ;

  puppet:
    ( z[i-1]!=z[i] && z[i]==z[i+1] --> z[i]:=z[i-1]; up[i]:=1; tok[i-1]:=0; tok[i]:=0; tok[i+1]:=1; )
    ( z[i-1]==z[i] && z[i]==z[i+1] && up[i+1]==0 -=> up[i]:=0; tok[i-1]:=1; tok[i]:=0; tok[i+1]:=0; )
    ;
}

process Top[i <- (N-1) + Nat % 1]
{
  write: fwd[0], tok[i-1], tok[i];

  read: z[i-1];
  write: up[i], z[i];

  shadow:
    ( tok[i]==1 --> fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; );

  puppet:
    ( true -=> z[i]:=z[i-1]; up[i]:=0; fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; );
}

