
constant N := 3;
constant M := 3;

shadow variable fwd[Nat % 1] <- Nat % 2;
shadow variable tok[Nat % N] <- Nat % 2;
puppet variable x[Nat % N] <- Nat % M;

((future & shadow) % puppet)
  (unique i <- Nat % N : tok[i]==1);

process Bot[i <- Nat % 1]
{
  write: x[i];
  read:  x[i+1];

  write: fwd[0], tok[i], tok[i+1];

  //(assume & closed) (x[i]!=2);

  shadow:
    ( tok[i]==1 --> fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; );

  puppet:
    ( x[i]!=1 && x[i+1]==2 --> x[i]:=1; fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i]!=0 && x[i+1]!=2 --> x[i]:=0; )
    ;
}

process P[i <- 1 + Nat % (N-2)]
{
  read: x[i-1];
  write: x[i];
  read: x[i+1];

  read: fwd[0];
  write: tok[i-1], tok[i], tok[i+1];

  shadow:
    ( fwd[0]==1 && tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; )
    ( fwd[0]==0 && tok[i]==1 --> tok[i-1]:=1; tok[i]:=0; )
    ;

  puppet:
    ( x[i-1]==1 && x[i]==0 && x[i+1]!=1 --> x[i]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==1 && x[i]==2              --> x[i]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==0 && x[i]==1 && x[i+1]==1 --> x[i]:=0; )
    ( x[i-1]==0 && x[i]==0 && x[i+1]==2 --> x[i]:=2; tok[i-1]:=1; tok[i]:=0; )
    ;
}

process Top[i <- (N-1) + Nat % 1]
{
  read: x[i-1];
  write: x[i];

  write: fwd[0], tok[i-1], tok[i];

  //(assume & closed) (x[i]!=0);

  shadow:
    ( tok[i]==1 --> fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; );

  puppet:
    ( x[i-1]==1 && x[i]!=1 --> x[i]:=1; )
    ( x[i-1]==0 && x[i]!=2 --> x[i]:=2; fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; )
    ( x[i-1]==2 && x[i]!=2 --> x[i]:=2; )
    ;
}

