
constant N := 3;
constant M := 3;

shadow variable fwd[1] < 2;
shadow variable tok[N] < 2;
puppet variable x[N] < M;

((future & shadow) % puppet)
  (unique i < N : tok[i]==1);

process Bot[i < 1]
{
  write: x[i];
  read:  x[i+1];

  write: fwd[0], tok[i], tok[i+1];

  //(assume & closed) (x[i]!=2);

  shadow:
    ( tok[i]==1 --> fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; );

  puppet:
    ( x[i]!=1 && x[i+1]==2 --> x[i]:=1; fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i]!=0 && x[i+1]!=2 --> x[i]:=0; )
    ;
}

process P[i <- map tid < N-2 : tid+1]
{
  read: x[i-1];
  write: x[i];
  read: x[i+1];

  read: fwd[0];
  write: tok[i-1], tok[i], tok[i+1];

  shadow:
    ( fwd[0]==1 && tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; )
    ( fwd[0]==0 && tok[i]==1 --> tok[i-1]:=1; tok[i]:=0; )
    ;

  puppet:
    ( x[i-1]==1 && x[i]==0 && x[i+1]!=1 --> x[i]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==1 && x[i]==2              --> x[i]:=1; tok[i]:=0; tok[i+1]:=1; )
    ( x[i-1]==0 && x[i]==1 && x[i+1]==1 --> x[i]:=0; )
    ( x[i-1]==0 && x[i]==0 && x[i+1]==2 --> x[i]:=2; tok[i-1]:=1; tok[i]:=0; )
    ;
}

process Top[i <- map tid < 1 : N-1]
{
  read: x[i-1];
  write: x[i];

  write: fwd[0], tok[i-1], tok[i];

  //(assume & closed) (x[i]!=0);

  shadow:
    ( tok[i]==1 --> fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; );

  puppet:
    ( x[i-1]==1 && x[i]!=1 --> x[i]:=1; )
    ( x[i-1]==0 && x[i]!=2 --> x[i]:=2; fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; )
    ( x[i-1]==2 && x[i]!=2 --> x[i]:=2; )
    ;
}

