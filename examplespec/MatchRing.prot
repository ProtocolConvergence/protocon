
// Matching ring protocol.
// Each process has a variable whose value represents a direction.
//  x[i] == 0  means P[i] is pointing to P[i-1].
//  x[i] == 1  means P[i] is pointing to itself.
//  x[i] == 2  means P[i] is pointing to P[i+1].
// Our goal is to end up in a state where processes are pointing to each other
// or, if both P[i-1] and P[i+1] are pointing away from P[i], then P[i] should
// point to itself.

constant N := 5;

variable x[Int % N] <- Int % 3;

process P[i <- Int % N]
{
  read: x[i-1];
  write: x[i];
  read: x[i+1];
  (future & silent)
    (   x[i-1] == 2 && x[i] == 0
     || x[i-1] == 0 && x[i] == 1 && x[i+1] == 2
     ||                x[i] == 2 && x[i+1] == 0
    );
}

