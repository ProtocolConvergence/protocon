
// Bidirectional token ring where each process has 3 states.
// Puppet topology originally proposed by Dijkstra.

constant N := 3;

shadow variable fwd[Nat % 1] <- Nat % 2;
shadow variable tok[Nat % N] <- Nat % 2;
puppet variable z[Nat % N] <- Nat % 3;

((future & active shadow) % puppet)
  (unique i <- Nat % N : tok[i]==1);

process Bot[i <- Nat % 1]
{
  write: fwd[0], tok[i], tok[i+1];

  write: z[i];
  read: z[i+1];

  shadow:
    ( tok[i]==1 --> fwd[0]:=1; tok[i]:=0; tok[i+1]:=1; )
    ;
}

process P[i <- 1 + Nat % (N-2)]
{
  read: fwd[0];
  write: tok[i-1], tok[i], tok[i+1];

  read: z[i-1];
  write: z[i];
  read: z[i+1];

  shadow:
    ( fwd[0]==1 && tok[i]==1 --> tok[i]:=0; tok[i+1]:=1; )
    ( fwd[0]==0 && tok[i]==1 --> tok[i-1]:=1; tok[i]:=0; )
    ;
}

process Top[i <- (N-1) + Nat % 1]
{
  write: fwd[0], tok[i-1], tok[i];

  read: z[i-1];
  write: z[i];
  read: z[i+1];

  shadow:
    ( tok[i]==1 --> fwd[0]:=0; tok[i-1]:=1; tok[i]:=0; )
    ;
}

